load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library", "rust_test")
load("@crates//:defs.bzl", "all_crate_deps")

# Rust SDK - zgrnet library
# KCP linked from //third_party/kcp (compiled with zig cc)
rust_library(
    name = "zgrnet",
    srcs = glob(["src/**/*.rs"]),
    crate_name = "zgrnet",
    edition = "2021",
    visibility = ["//visibility:public"],
    deps = all_crate_deps() + ["//third_party/kcp"],
)

rust_test(
    name = "zgrnet_test",
    crate = ":zgrnet",
    deps = all_crate_deps(normal_dev = True) + ["//third_party/kcp"],
)

# Library variant with TUN support (for integration tests requiring real TUN devices)
rust_library(
    name = "zgrnet_tun",
    srcs = glob(["src/**/*.rs"]),
    crate_name = "zgrnet",
    edition = "2021",
    crate_features = ["tun"],
    visibility = ["//visibility:public"],
    deps = all_crate_deps() + ["//third_party/kcp", "//zig/tun:lib"],
)

# TUN-specific tests (requires root/admin privileges)
rust_test(
    name = "tun_test",
    crate = ":zgrnet_tun",
    deps = all_crate_deps(normal_dev = True) + ["//third_party/kcp", "//zig/tun:lib"],
    env = {"RUST_TEST_THREADS": "1"},
    args = ["tun::"],
    tags = ["requires-root"],
)

# Library with TUN + dnsmgr (for zgrnetd binary)
rust_library(
    name = "zgrnet_full",
    srcs = glob(["src/**/*.rs"]),
    crate_name = "zgrnet",
    edition = "2021",
    crate_features = ["tun", "dnsmgr"],
    visibility = ["//visibility:public"],
    deps = all_crate_deps() + ["//third_party/kcp", "//zig/tun:lib", "//zig/dnsmgr:lib"],
)

# zgrnetd daemon
rust_binary(
    name = "zgrnetd",
    srcs = ["src/bin/zgrnetd.rs"],
    edition = "2021",
    crate_features = ["tun", "dnsmgr"],
    deps = [":zgrnet_full"] + all_crate_deps(),
    visibility = ["//visibility:public"],
)

# Host TUN integration test (requires root/admin privileges)
rust_binary(
    name = "host_test",
    srcs = ["examples/host_test.rs"],
    edition = "2021",
    crate_features = ["tun"],
    deps = [":zgrnet_tun"] + all_crate_deps(),
    tags = ["requires-root"],
)
