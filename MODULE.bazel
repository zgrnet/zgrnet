"""zgrnet - A networking library with Rust, Go, and Zig SDKs."""

module(
    name = "zgrnet",
    version = "0.1.0",
)

# Rust toolchain
bazel_dep(name = "rules_rust", version = "0.68.1")

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(edition = "2021")
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

# Rust crates
crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
crate.from_cargo(
    name = "crates",
    cargo_lockfile = "//rust:Cargo.lock",
    manifests = ["//rust:Cargo.toml"],
)
use_repo(crate, "crates")

# Go toolchain
bazel_dep(name = "rules_go", version = "0.59.0")
bazel_dep(name = "gazelle", version = "0.47.0")

go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk")
go_sdk.download(version = "1.23.4")

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//go:go.mod")
use_repo(
    go_deps,
    "org_golang_x_crypto",
)

# embed-zig: Bazel-native Zig build rules + Zig toolchain
bazel_dep(name = "embed_zig", version = "0.1.0")
git_override(
    module_name = "embed_zig",
    remote = "https://github.com/haivivi/embed-zig.git",
    commit = "b761fe7",
)

# Zig toolchain (provided by embed-zig, needed by //third_party/kcp for zig cc)
zig_toolchain = use_extension("@embed_zig//:extensions.bzl", "zig_toolchain")
use_repo(zig_toolchain, "zig_toolchain")

# KCP library (skywind3000/kcp)
kcp_lib = use_extension("//:extensions.bzl", "kcp_lib")
use_repo(kcp_lib, "kcp")

# Shell rules (Bazel 9 removed native sh_* rules; must use rules_shell)
bazel_dep(name = "rules_shell", version = "0.6.1")

# C/C++ rules (Bazel 9 removed native cc_* rules; must use rules_cc)
bazel_dep(name = "rules_cc", version = "0.2.16")

# Platform constraints
bazel_dep(name = "platforms", version = "1.0.0")

# Skylib for build settings
bazel_dep(name = "bazel_skylib", version = "1.9.0")

# BoringSSL
bazel_dep(name = "boringssl", version = "0.20251124.0")

# Minicoro - single-header coroutine library
http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
http_archive(
    name = "minicoro",
    urls = ["https://github.com/edubart/minicoro/archive/02dad0f8b7cbb12fe6e216ae7a76db15ca55cd7b.tar.gz"],
    strip_prefix = "minicoro-02dad0f8b7cbb12fe6e216ae7a76db15ca55cd7b",
    integrity = "sha256-fDYa+zYvaflWrAkLJZcZa3O6OCDg9+xNKXQWGxmuHFM=",
    build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_library")

cc_library(
    name = "minicoro",
    hdrs = ["minicoro.h"],
    visibility = ["//visibility:public"],
)
""",
)

# Wintun - Windows TUN driver
# https://www.wintun.net/
http_archive(
    name = "wintun",
    urls = ["https://www.wintun.net/builds/wintun-0.14.1.zip"],
    integrity = "sha256-J+swwQhZR5VvGgYjJYZ8J2F6V5Ej5x+CW+3Z7g6IxZw=",
    build_file_content = """
filegroup(
    name = "dll_amd64",
    srcs = ["wintun/bin/amd64/wintun.dll"],
    visibility = ["//visibility:public"],
)
filegroup(
    name = "dll_arm64",
    srcs = ["wintun/bin/arm64/wintun.dll"],
    visibility = ["//visibility:public"],
)
filegroup(
    name = "dll_x86",
    srcs = ["wintun/bin/x86/wintun.dll"],
    visibility = ["//visibility:public"],
)
filegroup(
    name = "header",
    srcs = ["wintun/include/wintun.h"],
    visibility = ["//visibility:public"],
)
""",
)
